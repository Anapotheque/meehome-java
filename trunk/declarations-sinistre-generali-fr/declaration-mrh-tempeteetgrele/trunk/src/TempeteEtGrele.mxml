<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:common="com.generali.views.*"
	 xmlns:comp="com.generali.tempeteEtGrele.views.*"  
	 xmlns:mx="http://www.adobe.com/2006/mxml" 	
	 preloader="com.generali.utils.CustomPreloader"
	 backgroundColor="white"  width="768"  height="620" layout="vertical" 
	 creationComplete="init()" preinitialize="startingUp()"
	 verticalScrollPolicy="auto" horizontalScrollPolicy="off" addedToStage="initTracker()">
	 
	 <mx:Style source="flex_skins.css"/>
	 
	 <mx:Script>
	 	<![CDATA[
	 	
	 		import com.generali.tracking.Tracker;
	 		import com.flexriver.common.logging.LogFactory;
	 		import com.generali.events.ui.InitApplicationEvent;
	 		import mx.controls.Alert;
	 		import com.generali.tempeteEtGrele.model.AppModel;
	 		import mx.controls.Button;
	 		import com.generali.events.ui.NextSectionEvent;
			import com.generali.utils.RecapGenerali;
			import com.generali.model.AddressVO;
			
			// Flex DeepLinking – permet de ecouter les changements au niveau de l'url
			import com.generali.utils.Util;
			import mx.utils.URLUtil;
			import mx.managers.BrowserManager;
			import mx.events.BrowserChangeEvent;
			import mx.managers.IBrowserManager;
			public var browserManager:IBrowserManager;
			
			private var _recapGenerali:RecapGenerali = new RecapGenerali();

        	[Embed(source="/assets/info_icon.png")]
        	private var _iconWarning:Class;

			[Embed(source="/assets/fred/stg_step_01.swf")]
        	private var _step1:Class;
        	
        	[Embed(source="/assets/fred/stg_step_02.swf")]
        	private var _step2:Class;
        	
        	[Embed(source="/assets/fred/stg_step_03.swf")]
        	private var _step3:Class;
        	
	 		[Bindable]
	 		private var widthComp:int = 715;
	 		[Bindable]
	 		private var heightComp:int= 520;
	 		
        	public const APPLICATION_TYPE:String = "TG";
        
        	[Bindable]
        	private var appModel:AppModel = AppModel.getInstance();
        	
        	private function startingUp():void
        	{
        		LogFactory.init();
        	}
        	
        	// Flex DeepLinking – permet de ecouter les changements au niveau de l'url    	
			private function parseURL(event:Event):void {
	            // Si le parametre "mode" est à "test" alors changement de l'adresse du client
	            if (URLUtil.stringToObject(browserManager.fragment).mode == Util.MODE_TEST )
					appModel.insured.mail = Util.SUIVI_PROD_MAIL_ESPACE_CLIENT;
        	}
        	
       		private function init():void {
       			// Flex DeepLinking – permet de ecouter les changements au niveau de l'url
				browserManager = BrowserManager.getInstance();
           		BrowserManager.getInstance().addEventListener(BrowserChangeEvent.BROWSER_URL_CHANGE, parseURL); 
           		browserManager.init("", "");
            	
				//On vérifie la bonne initialisation des 2 appels "FlashsVar" : CancelUrl et ServerUrl 
				//CancelUrl
				var urlCancel:String = Application.application.parameters.cancelUrl;
				if (urlCancel == "") {
					Alert.show(RecapGenerali.MSG_SERVER_INITIALISATION + "cancelUrl", "Initialisation Url", 0x4 ,null,null,_iconWarning);
				}
				//ServerUrl
				var urlServer:String = Application.application.parameters.serverUrl;
				if (urlServer == "") {
					Alert.show(RecapGenerali.MSG_SERVER_INITIALISATION + "serverUrl", "Initialisation Url", 0x4 ,null,null,_iconWarning);
				}

	 			this.addEventListener(NextSectionEvent.TO_SINISTRE,showSinistre);
	 			this.addEventListener(NextSectionEvent.TO_INSURED,showInsured);
	 			this.addEventListener(NextSectionEvent.TO_RECAP,showRecap);
	 			this.addEventListener(NextSectionEvent.TO_FINAL,showFinal);
	 			this.addEventListener(InitApplicationEvent.ID,clearAll);

				appModel.clear();
				
				//Pré-Initialisation des champs Assuré
				appModel.insured.last_name = Application.application.parameters.nom;
				appModel.insured.first_name = Application.application.parameters.prenom;
				appModel.insured.address.addressVal = Application.application.parameters.adresse;
				appModel.insured.address.postal_code = Application.application.parameters.codePostal;
				appModel.insured.address.city = Application.application.parameters.ville;
				appModel.insured.mail = Application.application.parameters.mail;
				appModel.insured.mobile_number = Application.application.parameters.telMobile;
				appModel.insured.house_number = Application.application.parameters.telDomicile;
				appModel.insured.work_number = Application.application.parameters.telBureau;
				appModel.insured.contract_number = Application.application.parameters.numeroContrat;
				appModel.insured.client_number= Application.application.parameters.numeroClient;
				appModel.insured.isCourtier = Application.application.parameters.isCourtier;
				appModel.residence.address.addressVal = Application.application.parameters.adresseBien;
				appModel.residence.address.postal_code = Application.application.parameters.codePostalBien;				
				appModel.residence.address.city = Application.application.parameters.villeBien;					
				if (Application.application.parameters.isResidencePrincipale == "true") {
					appModel.residence.main_residence = true;
				} else {
					appModel.residence.main_residence = false;					
				}
				
	 			showInsured(null);	 
	 			initChampsAssure();				
	 		}
	 		
			private function initChampsAssure():void {
				if (appModel.residence.address.addressVal != "" && appModel.residence.address.postal_code != "" && appModel.residence.address.city != "" ) {
					viewInsured.adresse_bien.text = appModel.residence.address.addressVal + " - " + appModel.residence.address.postal_code + " " + appModel.residence.address.city; 
				}
				
				viewInsured.adresse_bien.visible = false;
				viewInsured.lbl_Adresse_bien.visible = false;
				
				// SBO: Demande de la MOA d'activer en modification les tel. + email
				viewInsured.Q7.enabled = true;
				viewInsured.Q8.enabled = true;
				viewInsured.Q9.enabled = true;
				viewInsured.Q10.enabled = true;
			}
         
        	/**
	 		 *  Init the tracker 
	 		 */ 
	 		private function initTracker():void {
	 			Tracker.getInstance().trackView('mrhetape1lassure');
	 		}	 		


	 		private function showInsured(e:NextSectionEvent):void {
	 		    comp.source = _step1;
	 		 	this.currentState ="insured";
	 		}
	 		
	 		private function showSinistre(e:NextSectionEvent):void {
	 			comp.source = _step2;
	 		   	this.currentState ="sinistre";
		 		Tracker.getInstance().trackView('mrhetape2lesinistre');
	 		}
	 		
	 		private function showRecap(e:NextSectionEvent):void {	 			
	 		 	comp.source = _step3;
	 		  	this.currentState ="recap";
	 		  	Tracker.getInstance().trackView('mrhetape3recapitulatif');
	 		}
	 		
	 		private function showFinal(e:NextSectionEvent):void {
	 			 this.currentState ="final";
	 		 	 Tracker.getInstance().trackView('mrhetape4confirmation');
	 		}
	 		
	 		private function clearAll(e:InitApplicationEvent):void {
	 			appModel.clear();
	 		}

	 	]]>
	 </mx:Script>
	 
	 <mx:states>
	 	<mx:State name="insured"> 		   
				<mx:AddChild  relativeTo="{hb}" position="after"> 
					<common:Insured id="viewInsured" insuredModel="{appModel.insured}"  width="98%" height="100%"/>
		       	</mx:AddChild>
		</mx:State>
		<mx:State name="sinistre">		   
				 <mx:AddChild relativeTo="{hb}" position="after">
					 <comp:Accident  width="98%" height="100%"/>
		       	</mx:AddChild>
		</mx:State>
		<mx:State name="recap">		   
				 <mx:AddChild relativeTo="{hb}" position="after">
					 <comp:Recap  width="98%" height="100%"/>
		       	</mx:AddChild>
		</mx:State>
		<mx:State name="final">	
			 	 <mx:RemoveChild target="{comp}" />	   
			     <mx:AddChild relativeTo="{hb}" position="after">
					 <common:FinalStep insuredModel="{appModel.insured}" width="98%" height="100%"/>
		       	</mx:AddChild>
		</mx:State>
	 </mx:states>

	<mx:HBox width="753"  horizontalAlign="left" >
		<mx:Spacer width="45"/>
		<mx:Text   text="Habitation : tempête, grêle et neige" fontSize="17" fontFamily="Verdana" fontWeight="bold" color="#1F777D" />
	</mx:HBox> 
	
	<mx:Spacer height="2"/>
	
	<mx:HBox width="753" horizontalAlign="left" >
		<mx:Image id="comp" />
	</mx:HBox> 
	
	<mx:HBox id="hb" width="100%" >
		<mx:Spacer width="35" />
 	</mx:HBox> 	
 	
</mx:Application>
